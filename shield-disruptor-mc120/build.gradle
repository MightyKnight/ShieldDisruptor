archives_base_name = "shield-disruptor-1.20"

dependencies {
    minecraft "com.mojang:minecraft:1.20.6"
    mappings "net.fabricmc:yarn:1.20.6+build.1:v2"

    modImplementation "com.terraformersmc:modmenu:10.0.0-beta.1"
    modApi("me.shedaniel.cloth:cloth-config-fabric:14.0.126") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // ModMenu seems to need it
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.100.4+1.20.6"
}

// Minecraft 1.20.5 upwards uses Java 21
// But we need to use a lower version to also support 1.20-1.20.4 with the same code
tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

project.loom {
    runs {
        client {
            client()
            setConfigName("Fabric 1.20 Client")
            ideConfigGenerated(true)
            runDir("run/client-20")
        }
    }
}