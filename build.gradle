/*
Thanks to jacg311 and his project https://github.com/jacg311/shared-resources
for serving as a reference to creating this multi-version Gradle project
*/


plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
}

configurations.all {
    resolutionStrategy {
        force("net.fabricmc:fabric-loader:$loader_version")
    }
}

// stuff that applies to all projects
allprojects {
    apply plugin: "fabric-loom"

    // Java version is 8 for most versions, needs changing in submodules build.gradle for newer versions
    tasks.withType(JavaCompile).configureEach {
        it.options.release = 14
    }

    loom {
        // im not sure what this does,
        // but i think we might need it
        runConfigs.configureEach {
            ideConfigGenerated = true
        }
    }

    java {
        withSourcesJar()
    }

    version = project.mod_version
    group = project.maven_group

    // add repos for all projects
    repositories {

        maven {
            name "Terraformers Maven"
            url 'https://maven.terraformersmc.com'
        }
        maven {
            name "Shedaniel Maven"
            url "https://maven.shedaniel.me/"
        }

        maven {
            name "Jitpack - For FabricShieldLib"
            url = "https://jitpack.io"
        }

        maven {
            name "Modrinth Maven for Midnightlib, required by FabricShieldLib"
            url = "https://api.modrinth.com/maven"
        }

    }

    // floader needs to be in every sub module
    dependencies {
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    }

    processResources {
        inputs.property "version", version

        filesMatching("fabric.mod.json") {
            expand "version": version
        }
    }

    // According to modmuss: "To force the loader version for all deps"
    // https://discord.com/channels/507304429255393322/842691768175951942/1093276385742696488
    configurations.all {
        resolutionStrategy {
            force("net.fabricmc:fabric-loader:$loader_version")
        }
    }
}

dependencies {
    // dummy versions
    minecraft "com.mojang:minecraft:${project.common_minecraft_version}"
    mappings "net.fabricmc:yarn:${project.common_yarn_mappings}:v2"

    modCompileOnly "com.terraformersmc:modmenu:${project.common_modmenu_version}"
    modCompileOnly("me.shedaniel.cloth:cloth-config-fabric:${project.common_cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    //modCompileOnly("com.github.CrimsonDawn45:Fabric-Shield-Lib:${project.fabric_shield_lib_version}")
    include(implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixin_extras_version}")))

    subprojects.forEach((project) -> {
        include(project) {
            transitive = false
        }
    })
}

// stuff that applies to all sub projects
subprojects {

    dependencies {
        // include the root project because that's where our common code is
        implementation(project(path: ":", configuration: "namedElements")) {
            // having 2 floaders from the sub project and root project causes issues so we get rid of one.
            exclude group: "net.fabricmc", module: "fabric-loader"
        }
    }
}